@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Find required dependencies
find_dependency(glm)

# OpenGL is required - try CMake's FindOpenGL first, then platform defaults
find_package(OpenGL QUIET)
set(_vnm_opengl_target "")
if(TARGET OpenGL::OpenGL)
    set(_vnm_opengl_target "OpenGL::OpenGL")
elseif(TARGET OpenGL::GL)
    set(_vnm_opengl_target "OpenGL::GL")
endif()

if(NOT _vnm_opengl_target)
    # Provide fallback for systems without CMake OpenGL module
    if(WIN32)
        set(_vnm_opengl_lib "opengl32")
    elseif(APPLE)
        set(_vnm_opengl_lib "-framework OpenGL")
    else()
        if(CMAKE_SYSTEM_NAME STREQUAL "FreeBSD")
            find_file(_vnm_opengl_lib
                NAMES libOpenGL.so libOpenGL.so.0 libGL.so.1 libGL.so
                PATHS /usr/local/lib /usr/lib
            )
        else()
            find_library(_vnm_opengl_lib NAMES OpenGL GL)
        endif()
        if(NOT _vnm_opengl_lib)
            set(_vnm_opengl_lib "GL")
        endif()
    endif()
    set(_vnm_opengl_target "${_vnm_opengl_lib}")
    message(STATUS "vnm_plot: OpenGL targets not found, using platform default: ${_vnm_opengl_target}")
endif()

# Library naming (substituted at configure time)
set(_vnm_lib_prefix "@CMAKE_STATIC_LIBRARY_PREFIX@")
set(_vnm_lib_suffix "@CMAKE_STATIC_LIBRARY_SUFFIX@")

# Create imported targets if not already defined
set(_vnm_core_lib "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/${_vnm_lib_prefix}vnm_plot_core${_vnm_lib_suffix}")
if(NOT TARGET vnm_plot::core AND EXISTS "${_vnm_core_lib}")
    add_library(vnm_plot::core STATIC IMPORTED)
    set(_vnm_gl_dep "${_vnm_opengl_target}")
    set_target_properties(vnm_plot::core PROPERTIES
        IMPORTED_LOCATION "${_vnm_core_lib}"
        INTERFACE_INCLUDE_DIRECTORIES "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@"
        INTERFACE_LINK_LIBRARIES "glm::glm;${_vnm_gl_dep}"
    )
    unset(_vnm_gl_dep)
endif()

set(_vnm_qt_lib "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/${_vnm_lib_prefix}vnm_plot${_vnm_lib_suffix}")
if(NOT TARGET vnm_plot::vnm_plot AND EXISTS "${_vnm_qt_lib}")
    add_library(vnm_plot::vnm_plot STATIC IMPORTED)
    set_target_properties(vnm_plot::vnm_plot PROPERTIES
        IMPORTED_LOCATION "${_vnm_qt_lib}"
        INTERFACE_INCLUDE_DIRECTORIES "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@"
        INTERFACE_LINK_LIBRARIES "vnm_plot::core"
    )
endif()

unset(_vnm_lib_prefix)
unset(_vnm_lib_suffix)
unset(_vnm_core_lib)
unset(_vnm_qt_lib)
unset(_vnm_opengl_lib)
unset(_vnm_opengl_target)

check_required_components(vnm_plot)
